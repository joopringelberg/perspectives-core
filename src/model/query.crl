Context model:QueryAst als q:

import model:Perspectives als psp:

Section psp:rolInContext

--------------------------------------------------------------------------------
-- ASPECTEN
--------------------------------------------------------------------------------
psp:Context q:PropertyFunction
	$aspect => psp:Function
	$rolInContext =>
		psp:Rol $domain
			$aspectRol => psp:Function$domain
			$mogelijkeBinding => psp:Rol
	$rolInContext =>
		psp:Rol $range
			$aspectRol => psp:Function$range
			$mogelijkeBinding => psp:SimpleValue

psp:Context q:SingularPropertyFunction
	$aspect => q:PropertyFunction
	$rolInContext =>
		psp:Rol $range
			extern $isFunctioneel = true
			$aspectRol => q:PropertyFunction$range

psp:Context q:RolFunction
	$aspect => psp:Function
	$rolInContext =>
		psp:Rol $domain
			$aspectRol => psp:Function$domain
			$mogelijkeBinding => psp:Context
	$rolInContext =>
		psp:Rol $range
			$aspectRol => psp:Function$range
			$mogelijkeBinding =>
				psp:Sum $bindingsOfRange
					$alternative => psp:Context
					$alternative => psp:Rol

--------------------------------------------------------------------------------
-- SYSTEM GETTERS
--------------------------------------------------------------------------------
-- Identity is like Function (takes and returns any type) but yields a single result.
psp:Context q:identity
	$aspect => psp:Function
	$rolInContext =>
		psp:Rol $range
			extern $isFunctioneel = true
			$aspectRol => psp:Function$range

psp:SingularFunction q:contextType
	$domain => psp:Context
	$range => psp:Context

psp:SingularFunction q:buitenRol
	$domain => psp:Context
	$range => psp:Rol

psp:SingularFunction q:binnenRol
	$domain => psp:Context
	$range => psp:Rol

-- Returns the values of the rolInContext field of a PerspectContext. These are ids of
-- role instances.
psp:Function q:iedereRolInContext
	$domain => psp:Context
	$range => psp:Rol

-- Returns the keys of the rolInContext field of a PerspectContext. These are ids of Contexts
-- that describe RolTypes.
psp:Function q:rolTypen
	$domain => psp:Context
	$range => psp:Context

psp:SingularFunction q:rolType
	$domain => psp:Context
	$range => psp:Context

psp:SingularFunction q:binding
	$domain => psp:Rol
	$range => psp:Rol

psp:SingularFunction q:context
	$domain => psp:Rol
	$range => psp:Context

psp:SingularPropertyFunction q:label
	$rolInContext =>
		psp:Rol $range
			$aspectRol => q:SingularPropertyFunction$range
			$mogelijkeBinding => psp:String

--------------------------------------------------------------------------------
-- QUERY CONSTRUCTORS
--------------------------------------------------------------------------------
--q:Query q:useCache
--q:Query q:ignoreCache

psp:Context q:constructExternalPropertyGetter
	$aspect => q:PropertyFunction
	$rolInContext =>
		psp:Rol $property
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Property

-- Apply like this: q:constructExternalPropertyLookup $<propname>
psp:Context q:constructExternalPropertyLookup
	$aspect => q:PropertyFunction
	$rolInContext =>
		psp:Rol $property
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Property

psp:Context q:constructInternalPropertyGetter
	$aspect => q:PropertyFunction
	$rolInContext =>
		psp:Rol $property
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Property

-- Apply like this: q:constructInternalPropertyLookup $<propname>
psp:Context q:constructInternalPropertyLookup
	$aspect => q:PropertyFunction
	$rolInContext =>
		psp:Rol $property
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Property

psp:Context q:propertyQuery
	$aspect => q:PropertyFunction
	$rolInContext =>
		psp:Rol $property
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Property

psp:Context q:constructRolPropertyGetter
	$aspect => q:PropertyFunction
	$rolInContext =>
		psp:Rol $property
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Property

-- Apply like this: q:constructRolPropertyLookup $<propname>
psp:Context q:constructRolPropertyLookup
	$aspect => q:PropertyFunction
	$rolInContext =>
		psp:Rol $property
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Property

psp:Context q:rolQuery
	$aspect => q:RolFunction
	$rolInContext =>
		psp:Rol $rol
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol

-- Works only for declared roles. Can be replaced by q:rolQuery.
psp:Context q:constructRolGetter
	$aspect => q:RolFunction
	$rolInContext =>
		psp:Rol $rol
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol

-- Apply like this: q:constructRolLookup $<rolname>
-- Works only for declared roles. Can be replaced by q:constructRolGetter.
psp:Context q:constructRolLookup
	$aspect => q:RolFunction
	$rolInContext =>
		psp:Rol $rol
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol

psp:Context q:constructInverseRolGetter
	$aspect => q:RolFunction
	$rolInContext =>
		psp:Rol $rol
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol

-- It is unlikely we will use this constructor in a model (but maybe in a query or statement?)
psp:Context q:iedereRolInContext
	$aspect => q:RolFunction
	$rolInContext =>
		psp:Rol $context
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Context

--------------------------------------------------------------------------------
-- COMBINATORS
--------------------------------------------------------------------------------
psp:Context q:laatste
	$rolInContext =>
		psp:Rol $query
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Function

psp:Context q:compose
	$rolInContext =>
		psp:Rol $operand
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Function

-- notEmpty is itself not a Criterium, but the application of notEmpty to a Query is.
psp:Context q:notEmpty
	$rolInContext =>
		psp:Rol $query
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Function

psp:Context q:closure
	$rolInContext =>
		psp:Rol $query
			extern $isFunctioneel = true
			$mogelijkeBinding => q:RolFunction

psp:Context q:closure'
	$rolInContext =>
	 	psp:Rol $query
			extern $isFunctioneel = true
			$mogelijkeBinding => q:RolFunction

psp:Context q:filter
	$rolInContext =>
	 	psp:Rol $criterium
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Constraint
	$rolInContext =>
		psp:Rol $candidates
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Function

psp:Context q:concat
	$rolInContext =>
		psp:Rol $operand
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Function

psp:Context q:rolesOf
	$rolInContext =>
		psp:Rol $context
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Context
--------------------------------------------------------------------------------
-- COMBINATOR CONSTRUCTORS
--------------------------------------------------------------------------------
-- It is unlikely we will use this constructor in a model.
q:Context q:contains
	$rolInContext =>
		psp:Rol $valueOrId
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Context
	$rolInContext =>
		psp:Rol $query
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Function

--------------------------------------------------------------------------------
-- CONSTANT
--------------------------------------------------------------------------------
psp:Rol q:Constant
	$internalProperty =>
		psp:Property $value
			extern $isFunctioneel = true
			extern $isVerplicht = true
			$range => psp:String

--------------------------------------------------------------------------------
-- VARIABLE
--------------------------------------------------------------------------------
psp:Context q:Variable
	$internalProperty =>
		psp:Property $name
			extern $isFunctioneel = true
			extern $isVerplicht = true
			$range => psp:String

psp:Context q:setVariable
	$internalProperty =>
		psp:Property $name
			extern $isFunctioneel = true
			extern $isVerplicht = true
			$range => psp:String
	$rolInContext =>
		psp:Rol $value
			extern $isFunctioneel = true
			$mogelijkeBinding => q:RolFunction
			$mogelijkeBinding => q:PropertyFunction
			$mogelijkeBinding => q:Variable
			$mogelijkeBinding => psp:Context

--------------------------------------------------------------------------------
-- ASSIGNMENT
--------------------------------------------------------------------------------
-- We add to the context of the actie that this statement occurs in.
-- addRol :: forall e. ContextID -> RolName -> RolID -> MonadTransactie e Unit
-- Apply like this for the statement: "getuige += verdachte":
--	assignToRol <gen>
--		intern $operation = "add"
--		$rol => pol:Aangifte$getuige
--		$value =>
--			q:constructRolLookup <gen>
--				$rol => pol:Aangifte$verdachte
psp:Context q:assignToRol
	$internalProperty =>
		psp:Property $operation
			extern $isFunctioneel = true
			extern $isVerplicht = true
			-- possible values are: "add", "remove", "set"
			$range => psp:String
	$rolInContext =>
		-- The rol we will assign to. The qualified name is used!
		psp:Rol $rol
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol
	$rolInContext =>
		-- The rol instance we will add, delete or set:
		psp:Rol $value
			extern $isFunctioneel = true
			$mogelijkeBinding => q:RolFunction
			$mogelijkeBinding => q:Variable
			$mogelijkeBinding => psp:Context

-- We add to the context of the actie that this statement occurs in.
-- assignToProperty :: forall e. RolID -> PropertyName -> Value -> MonadTransactie e Unit
-- Apply like this for the statement: 'voornaam van getuige += "Jan"':
--	assignToProperty <gen>
--		intern $operation = "add"
--		$rol => pol:Aangifte$getuige
--		$property => psp:Persoon$voornaam
--		$value =>
--			q:Constant "Jan"
-- Apply like this for the statement: 'voornaam van getuige += verdachte voornaam':
--	assignToProperty <gen>
--		intern $operation = "add"
--		$rol => pol:Aangifte$getuige
--		$property => psp:Persoon$voornaam
--		$value =>
--			q:compose $xxx
--				$operand (1) =>
--					q:constructRolLookup $verdachte
--				$operand (2) =>
--					q:constructExternalPropertyLookup $voornaam
psp:Context q:assignToProperty
	$internalProperty =>
		psp:Property $operation
			extern $isFunctioneel = true
			extern $isVerplicht = true
			-- possible values are: "add", "remove", "set"
			$range => psp:String
	$internalProperty =>
		psp:Property $constantValue
			extern $isFunctioneel = true
			extern $isVerplicht = false
			$range => psp:String
	$rolInContext =>
		-- The rol we will change a property of. The qualified name is used!
		psp:Rol $rol
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol
	$rolInContext =>
		-- The property we will change. The qualified name is used!
		psp:Rol $property
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Property
	$rolInContext =>
		-- The property query that gives the value:
		psp:Rol $value
			extern $isFunctioneel = true
			$mogelijkeBinding => q:PropertyFunction
			$mogelijkeBinding => q:Variable
