Context model:Perspectives als psp:

Section $rolInContext

--------------------------------------------------------------------------------
-- SIMPLE VALUES
--------------------------------------------------------------------------------
psp:Context psp:SimpleValue
	prototype psp:ContextPrototype
	--$aspect => psp:SimpleValue
	$aspect => psp:Context

psp:SimpleValue psp:Boolean
	prototype psp:ContextPrototype

psp:SimpleValue psp:String
	prototype psp:ContextPrototype

psp:SimpleValue psp:Number
	prototype psp:ContextPrototype

psp:SimpleValue psp:Date
	prototype psp:ContextPrototype

--------------------------------------------------------------------------------
-- SYSTEM BASE
--------------------------------------------------------------------------------
psp:Context psp:Property
	$buitenRolBeschrijving =>
		psp:Rol $buitenRolBeschrijving
			extern $isFunctioneel = true
			$rolProperty =>
				psp:Property $isFunctioneel
					extern $isFunctioneel = true
					extern $isVerplicht = true
					$range => psp:Boolean
			$rolProperty =>
				psp:Property $isVerplicht
					extern $isFunctioneel = true
					extern $isVerplicht = true
					$range => psp:Boolean
			$mogelijkeBinding => psp:ElkType
	$binnenRolBeschrijving =>
		psp:Rol $binnenRolBeschrijving
			-- (RolWithoutContext) De Rol 'model:Perspectives$Property$binnenRolBeschrijving' heeft geen definiërende Context.
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol
	$rolInContext =>
		psp:Rol $range
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:SimpleValue
	$rolInContext =>
		psp:Rol $aspectProperty
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Property
	$rolInContext =>
		psp:Rol $bindingProperty
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Property

psp:Context psp:Context
	prototype psp:ContextPrototype
	$rolInContext =>
		psp:Rol $binnenRolBeschrijving
			extern $isFunctioneel = true
			extern $isVerplicht = true
			$mogelijkeBinding => psp:Rol
	$rolInContext =>
		psp:Rol $buitenRolBeschrijving
			extern $isFunctioneel = true
			extern $isVerplicht = true
			$mogelijkeBinding => psp:Rol
	$rolInContext =>
		psp:Rol $rolInContext
			extern $isFunctioneel = false
			$mogelijkeBinding =>
				psp:Sum $RolOrContext
					$alternative => psp:Rol
					-- Hier moet denk ik een query bij: dat is een Function.
					$alternative => psp:Context
	$rolInContext =>
		psp:Rol $interneView
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:View
	$rolInContext =>
		psp:Rol $externeView
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:View
	$rolInContext =>
		psp:Rol $prototype
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Context
	$rolInContext =>
		psp:Rol $aspect
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:ElkType
	$rolInContext =>
		psp:Rol $gebruikerRol
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Systeem$gebruiker
	$rolInContext =>
		psp:Rol $contextBot
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:SysteemBot

psp:Context psp:ContextPrototype
	$binnenRolBeschrijving =>
		psp:Rol $binnenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol
	$buitenRolBeschrijving =>
		psp:Rol $buitenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol

psp:Context psp:Rol
	$binnenRolBeschrijving =>
		psp:Rol $binnenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol
	$buitenRolBeschrijving =>
		psp:Rol $buitenRolBeschrijving
			extern $isFunctioneel = true
			$rolProperty =>
				psp:Property $isFunctioneel
					extern $isFunctioneel = true
					extern $isVerplicht = true
					$range => psp:Boolean
			$rolProperty =>
				psp:Property $isVerplicht
					extern $isFunctioneel = true
					extern $isVerplicht = false
					$range => psp:Boolean
			$mogelijkeBinding => psp:ElkType
	$rolInContext =>
		psp:Rol $rolProperty
			extern $isFunctioneel = false
			$mogelijkeBinding =>
				psp:Sum $FunctionOrProperty
					$alternative => psp:Function
					$alternative => psp:Property
	$rolInContext =>
		psp:Rol $mogelijkeBinding
			extern $isFunctioneel = true
			extern $isVerplicht = true
			$mogelijkeBinding => psp:ElkType
	$rolInContext =>
		psp:Rol $viewInRol
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:View
	$rolInContext =>
		psp:Rol $aspectRol
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Rol
	$rolInContext =>
		psp:Rol $constraint
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Function
	$rolInContext =>
		psp:Rol $objectRol
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Actie
	$rolInContext =>
		psp:Rol $subjectRol
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Actie

psp:Context psp:SysteemBot
	$binnenRolBeschrijving =>
		psp:Rol $binnenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol
	$buitenRolBeschrijving =>
		psp:Rol $buitenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:ElkType
	$rolInContext =>
		psp:Rol $rolProperty
			extern $isFunctioneel = false
			$mogelijkeBinding =>
				psp:Sum $FunctionOrProperty
					$alternative => psp:Function
					$alternative => psp:Property
	$rolInContext =>
		psp:Rol $subjectRol
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Actie

psp:Context psp:View
	prototype psp:ContextPrototype
	$rolInContext =>
		psp:Rol $propertyReferentie
			extern $isVerplicht = true
			extern $isFunctioneel = false
			$rolProperty =>
				psp:Property $volgNummer
					extern $isFunctioneel = true -- ziet ie niet
					extern $isVerplicht = true -- ziet ie niet
					$range => psp:Number
			$mogelijkeBinding => psp:Property

--------------------------------------------------------------------------------
-- ACTIES
--------------------------------------------------------------------------------
psp:Context psp:Actie
	prototype psp:ContextPrototype
	$rolInContext =>
		psp:Rol $subjectView
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:View
	$rolInContext =>
		psp:Rol $werkwoord
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Werkwoord
	$rolInContext =>
		psp:Rol $objectView
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:View
	$rolInContext =>
		psp:Rol $objectConstraint
			extern $isFunctioneel = true
			extern $isVerplicht = false
			$mogelijkeBinding => psp:Function
	$rolInContext =>
		psp:Rol $subjectConstraint
			extern $isFunctioneel = true
			extern $isVerplicht = false
			$mogelijkeBinding => psp:Function
	$rolInContext =>
		psp:Rol $condition
			extern $isFunctioneel = true
			extern $isVerplicht = false
			$mogelijkeBinding => psp:Function
	$rolInContext =>
		psp:Rol $effect
			extern $isFunctioneel = true
			extern $isVerplicht = false
			$mogelijkeBinding =>
				psp:Sum $effectFun
					$alternative (1) => psp:assignToRol
					$alternative (2) => psp:assignToProperty
					$alternative (3) => psp:effectFullFunction

psp:Context psp:Werkwoord
	prototype psp:ContextPrototype
	$aspect => psp:Context

psp:Werkwoord psp:Beheert
	prototype psp:ContextPrototype

psp:Werkwoord psp:Raadpleegt
	prototype psp:ContextPrototype

psp:Werkwoord psp:Creëert
	prototype psp:ContextPrototype

psp:Werkwoord psp:Verwijdert
	prototype psp:ContextPrototype

--------------------------------------------------------------------------------
-- ZAAK
--------------------------------------------------------------------------------
psp:Context psp:Zaak
	prototype psp:ContextPrototype
	$aspect => psp:Context
	$rolInContext =>
		psp:Rol $zaakInContext
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Zaak
	$rolInContext =>
		psp:Rol $actieInContext -- TODO: gewoon 'actie'?
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:Actie

--------------------------------------------------------------------------------
-- FUNCTION
--------------------------------------------------------------------------------
psp:Context psp:Function
	prototype psp:ContextPrototype
	$aspect => psp:Context

--------------------------------------------------------------------------------
-- UNIT TYPE: ELKTYPE
--------------------------------------------------------------------------------
psp:Context psp:ElkType
	prototype psp:ContextPrototype

--------------------------------------------------------------------------------
-- SUM TYPE
--------------------------------------------------------------------------------
psp:Context psp:Sum
	prototype psp:ContextPrototype
	$rolInContext =>
		psp:Rol $alternative
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:ElkType

	--------------------------------------------------------------------------------
	-- SYSTEEM
	--------------------------------------------------------------------------------

	psp:Context psp:Systeem
		$binnenRolBeschrijving =>
			psp:Rol $binnenRolBeschrijving
				extern $isFunctioneel = true
				$mogelijkeBinding => psp:Rol
		$buitenRolBeschrijving =>
			psp:Rol $buitenRolBeschrijving
				extern $isFunctioneel = true
				$mogelijkeBinding => psp:Rol
				$rolProperty =>
					psp:Property $modelOphaalTeller
						extern $isFunctioneel = true
						extern $isVerplicht = true
						$range => psp:Number
		$rolInContext =>
			psp:Rol $trustedCluster
				extern $isFunctioneel = true
				$mogelijkeBinding => psp:TrustedCluster
		$rolInContext =>
			psp:Rol $gebruiker
				-- wat is de mogelijkeBinding van gebruiker?
				-- we gaan niets binden, dus elk type is goed.
				extern $isFunctioneel = true
				$mogelijkeBinding => psp:ElkType
				$viewInRol =>
					psp:View $VolledigeNaam
						$propertyReferentie => $$voornaam
							$volgNummer = 1
						$propertyReferentie => $$achternaam
							$volgNummer = 2
				$rolProperty =>
					psp:Property $achternaam
						extern $isFunctioneel = false
						extern $isVerplicht = true
						$range => psp:String
				$rolProperty =>
					psp:Property $voornaam
						extern $isFunctioneel = false
						extern $isVerplicht = true
						$range => psp:String

	psp:Zaak psp:TrustedCluster
		psp:Context$buitenRolBeschrijving =>
			psp:Rol $buitenRolBeschrijving
				extern $isFunctioneel = true
				$viewInRol =>
					psp:View $Kaartje
						$propertyReferentie => $$naam
							$volgNummer = 1
				$rolProperty =>
					psp:Property $naam
						extern $isFunctioneel = true
						extern $isVerplicht = true
						$range => psp:String
				$mogelijkeBinding => psp:Rol
		psp:Context$binnenRolBeschrijving =>
			psp:Rol $binnenRolBeschrijving
				extern $isFunctioneel = true
				$mogelijkeBinding => psp:Rol
		psp:Context$rolInContext =>
			psp:Rol $clusterGenoot
				extern $isFunctioneel = false
				$mogelijkeBinding => psp:Systeem$gebruiker
				$rolProperty =>
					psp:Property $url
						extern $isFunctioneel = true
						extern $isVerplicht = true
						$range => psp:String
				$viewInRol =>
					psp:View $Adressering
						$propertyReferentie => $$url
							$volgNummer = 1
						$propertyReferentie => psp:Systeem$gebruiker$voornaam
							$volgNummer = 2
				$objectRol => $$RaadpleegtClusterGenoot
				$subjectRol => $$RaadpleegtClusterGenoot
		$actieInContext =>
			psp:Actie $RaadpleegtClusterGenoot
				$werkwoord => psp:Raadpleegt
				$objectView => $$clusterGenoot$Adressering

--------------------------------------------------------------------------------
-- ASSIGNMENT
--------------------------------------------------------------------------------
-- We add to the context of the actie that this statement occurs in.
-- addRol :: forall e. ContextID -> RolName -> RolID -> MonadTransactie e Unit
-- Apply like this for the statement: "getuige += verdachte":
--	assignToRol <gen>
--		extern $operation = "add"
--		$rol => pol:Aangifte$getuige
--		$value =>
--			q:constructRolLookup <gen>
--				$rol => pol:Aangifte$verdachte
psp:Context psp:assignToRol
	$buitenRolBeschrijving =>
		psp:Rol $buitenRolBeschrijving
			extern $isFunctioneel = true
			$rolProperty =>
				psp:Property $operation
					extern $isFunctioneel = true
					extern $isVerplicht = true
					$range => psp:String
			$mogelijkeBinding => psp:Rol
	$binnenRolBeschrijving =>
		psp:Rol $binnenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:ElkType
	$rolInContext =>
		psp:Rol $rol
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol
	$rolInContext =>
		psp:Rol $value
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Function

-- We add to the context of the actie that this statement occurs in.
-- assignToProperty :: forall e. RolID -> PropertyName -> Value -> MonadTransactie e Unit
-- Apply like this for the statement: 'voornaam van getuige += "Jan"':
--	assignToProperty <gen>
--		extern $operation = "add"
--		$rol => pol:Aangifte$getuige
--		$property => psp:Persoon$voornaam
--		$value =>
--			q:Constant "Jan"
-- Apply like this for the statement: 'voornaam van getuige += verdachte voornaam':
--	assignToProperty <gen>
--		extern $operation = "add"
--		$rol => pol:Aangifte$getuige
--		$property => psp:Persoon$voornaam
--		$value =>
--			q:compose $xxx
--				$operand (1) =>
--					q:constructRolLookup $verdachte
--				$operand (2) =>
--					q:DataTypeGetter <gen>
--						extern $functionName = "context"
--				$operand (3) =>
--					q:constructExternalPropertyLookup $voornaam
psp:Context psp:assignToProperty
	$buitenRolBeschrijving =>
		psp:Rol $buitenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol
			$rolProperty =>
				psp:Property $operation
					extern $isFunctioneel = true
					extern $isVerplicht = true
					-- possible values are: "add", "remove", "set"
					$range => psp:String
			$rolProperty =>
				psp:Property $constantValue
					extern $isFunctioneel = true
					extern $isVerplicht = false
					$range => psp:String
	$binnenRolBeschrijving =>
		psp:Rol $binnenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:ElkType
	$rolInContext =>
		-- The property we will change. The qualified name is used!
		psp:Rol $property
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Property
	$rolInContext =>
		-- The property query that gives the value:
		psp:Rol $value
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Function

-- Apply an effectful function like storeDomeinFileInCouchdb as follows:
-- storeDomeinFileInCouchdb :: forall e. DomeinFile -> MonadPerspectives (AjaxAvarCache e) Unit
-- The Action is for the bot that plays a role in a model:CrlText$Text context.
-- The contextId that the action applies to identifies this context, hence we need no parameters when calling this function.
--	psp:effectFullFunction <gen>
--		extern $functionName = "storeDomeinFileInCouchdb"

psp:Context psp:effectFullFunction
	$buitenRolBeschrijving =>
		psp:Rol $buitenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:Rol
			$rolProperty =>
				psp:Property $functionName
					extern $isFunctioneel = true
					extern $isVerplicht = true
					$range => psp:String
	$binnenRolBeschrijving =>
		psp:Rol $binnenRolBeschrijving
			extern $isFunctioneel = true
			$mogelijkeBinding => psp:ElkType
	$rolInContext =>
		-- The parameters of the effectful function.
		psp:Rol $parameter
			extern $isFunctioneel = false
			$mogelijkeBinding => psp:ElkType
